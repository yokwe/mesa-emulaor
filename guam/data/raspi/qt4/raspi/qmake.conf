#
# qmake configuration for linux-g++
#

MAKEFILE_GENERATOR  = UNIX
TARGET_PLATFORM     = unix
TEMPLATE            = app
CONFIG              += qt warn_on release incremental link_prl gdb_dwarf_index
QT                  += core gui
QMAKE_INCREMENTAL_STYLE = sublib

#First official sd-card release
SYSROOT    = /mnt/raspi
GCC_PREFIX = /opt/raspi/tools/arm-bcm2708/gcc-linaro-arm-linux-gnueabihf-raspbian/bin/arm-linux-gnueabihf


QT_INSTALL_HEADERS  = $${SYSROOT}/usr/include/qt4

include(../common/linux.conf)
include(../common/gcc-base-unix.conf)
include(../common/g++-unix.conf)

QT_INSTALL_HEADERS = $${SYSROOT}/usr/include/qt4

#QMAKE_COMPILER          = gcc

INCLUDEPATH             += $${SYSROOT}/usr/include/arm-linux-gnueabihf
INCLUDEPATH             += $${SYSROOT}/usr/include

# modifications to g++.conf
QMAKE_CC                = $${GCC_PREFIX}-gcc --sysroot=$${SYSROOT}
QMAKE_CXX               = $${GCC_PREFIX}-g++ --sysroot=$${SYSROOT}
QMAKE_LINK              = $${QMAKE_CXX}
QMAKE_LINK_SHLIB        = $${QMAKE_CXX}

# modifications to linux.conf
QMAKE_AR                = $${GCC_PREFIX}-ar cqs
QMAKE_OBJCOPY           = $${GCC_PREFIX}-objcopy
QMAKE_STRIP             = $${GCC_PREFIX}-strip

INCLUDEPATH += $${SYSROOT}/opt/vc/include $${SYSROOT}/usr/include
#udev libs present, header absent
##INCLUDEPATH += /opt/dev/src/qt-platform-mkspecs/5.0/linux-rasp-pi-g++/header-taint

QMAKE_LIBS_EGL=-L$${SYSROOT}/opt/vc/lib -lWFC -lGLESv2 -lEGL -lbcm_host -lopenmaxil
QMAKE_LIBS_OPENGL_ES2=$${QMAKE_LIBS_EGL}
QMAKE_LIBS_OPENVG=$${QMAKE_LIBS_EGL}

#This is an initial set of compiler flags yet to be validated: YMMV
QMAKE_CFLAGS_RELEASE =  \
                        -mfloat-abi=hard \
                        -mfpu=vfp \
                        -fomit-frame-pointer \
                        -Wall \
#bcm specific
                        -O2 \
                        -mtune=arm1176jzf-s \
                        -march=armv6zk \
                        -mabi=aapcs-linux

QMAKE_CXXFLAGS_RELEASE = $$QMAKE_CFLAGS_RELEASE

DEFINES += BCM_PI

load(qt_config)
